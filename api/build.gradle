plugins {
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
}

apply plugin: 'java-library'
apply plugin: "com.vanniktech.maven.publish"

dependencies {
    api libs.jacksonAnnotations
    api libs.jacksonDatabind
    api libs.jtokkit
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testImplementation libs.jacksonDatabind
    testImplementation(platform(libs.junitBom))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

test {
    useJUnitPlatform()
}

task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
}
